apply plugin: 'com.android.application'
apply plugin: 'com.getkeepsafe.dexcount' //查看方法数工具
apply from:"../config/properties-util.gradle"
android {
    compileSdkVersion rootProject.ext.compileSdkVersion     //在项目中的build.gradle ext{}里面
    buildToolsVersion rootProject.ext.buildToolsVersion

    defaultConfig {
        applicationId rootProject.ext.applicationId
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode rootProject.ext.versionCode
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        versionName rootProject.ext.versionName
        //设置常量  API_SERVER_URL
        buildConfigField("String", "API_SERVER_URL", '"https://www.oschina.net/;http://www.oschina.cc/"')
        buildConfigField("String", "VIOLET_PASSCODE", '"violet_passcode"')
    }
    // rename the apk with the version name
    applicationVariants.all { variant ->
        variant.outputs.each { output ->
            output.outputFile = new File(
                    output.outputFile.parent + "/${variant.buildType.name}",
                    "dome-osc-android-${variant.versionName}-${variant.buildType.name}.apk".toLowerCase())
        }
    }
    //signing files settings
    signingConfigs {
        if (propertyHaveSigningConfigs) {       //  根目录/config/properties-util.gradle
            debug {
                storeFile file(propertyStoreFileStr)
                storePassword propertyStorePwdStr
                keyAlias propertyKeyAliasStr
                keyPassword propertyKeyPwdStr
            }

            release {
                storeFile file(propertyStoreFileStr)
                storePassword propertyStorePwdStr
                keyAlias propertyKeyAliasStr
                keyPassword propertyKeyPwdStr
            }
        }
    }

    sourceSets { //jni进入项目
        main {
            jniLibs.srcDirs = ['libs']
        }
    }

    // 移除lint检查的error
    lintOptions {
        abortOnError false
    }

    //build type setting
    buildTypes {

        debug {
            zipAlignEnabled false
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            if (propertyHaveSigningConfigs)
                signingConfig signingConfigs.release
        }

        release {
            minifyEnabled true  //混淆
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            zipAlignEnabled true  //zip算法压缩
            if (propertyHaveSigningConfigs)
                signingConfig signingConfigs.release
        }
    }

    //product flavors
    productFlavors {
        oschina {
            manifestPlaceholders = [UMENG_CHANNEL: "oschina"]
        }
    }
}


repositories {
    flatDir {   //使用本地文件夹,如果jcenter也有,优先使用仓库服务器的
        dirs 'libs' //library-2.4.0 是动画框架//https://blog.csdn.net/fjekin/article/details/51899850
    }
    if (propertyHaveDebugCompile) {  //debug版本使用这个 --这个debug也为false
        maven {
            url propertyDebugCompileUrl
        }
    }
}

dependencies {
    compile "com.android.support:support-v4:$rootProject.ext.supportVersion"    //支持v4的一些功能(例Fragment)
    compile "com.android.support:appcompat-v7:$rootProject.ext.supportVersion"  //支持v7下的一些功能(例SearchView)
    compile "com.android.support:design:$rootProject.ext.supportVersion"    //支持design
    compile "com.android.support:recyclerview-v7:$rootProject.ext.supportVersion"   //recyclerview引用
    compile "com.android.support:cardview-v7:$rootProject.ext.supportVersion"   //cardview的引用
    compile fileTree(include: ['*.jar'], dir: 'libs')   //包含libs下的jar包进行引用
    compile project(':open')      //open项目的引用 --- 引用了arr包
    if (propertyHaveDebugCompile) {     //debug版本使用这个 --这个debug也为false
        compile propertyDebugCompile
    }
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })  //android测试框架
    compile 'com.github.chrisbanes.photoview:library:1.2.4' //相片框架
    compile 'com.loopj.android:android-async-http:1.4.9'    //网络框架
    compile 'com.jakewharton:butterknife:7.0.1'     //butterknife view注入框架
    compile 'com.google.zxing:core:3.3.0'       //扫描框架
    compile 'com.joanzapata.android:android-iconify:1.0.9'  //矢量图标库 IconTextView、IconButton、IconToggleButton
    compile 'com.makeramen:roundedimageview:2.1.1'  //图片的圆形处理框架
    compile 'pub.devrel:easypermissions:0.3.0'  //简单申请权限框架
    compile 'com.github.bumptech.glide:glide:3.7.0' //图片框架
    compile 'de.hdodenhof:circleimageview:2.0.0'    //图片的圆形处理框架
    compile 'com.google.code.gson:gson:2.8.0'   //json解析框架
    compile 'com.umeng.analytics:analytics:latest.integration'  //友盟统计框架
    compile 'net.oschina.common:common:0.1.6'   //开源中国公共框架
    compile 'com.belerweb:pinyin4j:2.5.1'   //中文转换成拼音
    testCompile 'junit:junit:4.12'  //测试框架
}
